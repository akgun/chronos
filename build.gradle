ext {
	guice = 'com.google.inject:guice:4.0'
	gson = 'com.google.code.gson:gson:2.4'
	joda = 'joda-time:joda-time:2.8.2'
	junit = 'junit:junit:4.12'

	version = '1.0'
}


allprojects {
	group = 'com.akgund.chronos'
	version = version
}

subprojects {
	apply plugin: 'java'

	sourceCompatibility = 1.8
	targetCompatibility = 1.8

	repositories {
		mavenCentral()
	}
}

project(':chronos-core') {
	dependencies {
		compile guice
		compile gson
		compile joda

		testCompile junit
	}
}

project(':chronos-util') {
	dependencies {
		compile joda

		testCompile junit
	}
}

project(':chronos-gui') {
	dependencies {
		compile project(':chronos-core')
		compile project(':chronos-util')

		compile joda
	}

	task fatJar(type: Jar) {
	    manifest {
	        attributes 	'Main-Class': 'com.akgund.chronos.gui.Program', 
	        			'Version': rootProject.ext.version
	    }

	    baseName = rootProject.name
	    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
	    with jar
	}

	build.dependsOn fatJar
}